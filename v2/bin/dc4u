#!/usr/bin/env perl

use strict;
use warnings;
use v5.32;

use FindBin;
use lib "$FindBin::Bin/../lib";

use DC4U;
use Getopt::Long;
use Pod::Usage;
use File::Basename;

=head1 NAME

dc4u - Draft Charges 4 U - Legal Document Generator

=head1 SYNOPSIS

    dc4u [options] [input_file ...]

    Options:
        --format, -f FORMAT    Output format (PDF, HTML, TXT, MD, RMD, DOCX)
        --output, -o FILE     Output file
        --config, -c FILE     Configuration file
        --jurisdiction, -j J  Jurisdiction (singapore, uk)
        --batch, -b           Batch processing mode
        --verbose, -v         Verbose output
        --quiet, -q           Quiet mode
        --help, -h            Show help
        --version            Show version

    When invoked with no --format flag and no input files, launches
    the interactive TUI mode.

=cut

# command line options
my $format;
my $output_file;
my $config_file;
my $jurisdiction;
my $batch_mode = 0;
my $verbose = 0;
my $quiet = 0;
my $help = 0;
my $version = 0;
my $format_given = 0; # track if --format was explicitly set

GetOptions(
    'format|f=s' => sub { $format = $_[1]; $format_given = 1; },
    'output|o=s' => \$output_file,
    'config|c=s' => \$config_file,
    'jurisdiction|j=s' => \$jurisdiction,
    'batch|b' => \$batch_mode,
    'verbose|v' => \$verbose,
    'quiet|q' => \$quiet,
    'help|h' => \$help,
    'version' => \$version
) or pod2usage(2);

if ($help) {
    pod2usage(1);
}

if ($version) {
    print "DC4U Version 2.0.0\n";
    exit 0;
}

# validate jurisdiction if given
if ($jurisdiction) {
    my @valid_j = qw(singapore uk australia india malaysia);
    unless (grep { $_ eq $jurisdiction } @valid_j) {
        print "Error: Invalid jurisdiction '$jurisdiction'. Valid: " . join(', ', @valid_j) . "\n";
        exit 1;
    }
}

my @input_files = @ARGV;

# launch TUI if no --format flag and no input files
if (!$format_given && @input_files == 0) {
    require DC4U::TUI;
    my $tui = DC4U::TUI->new(config_file => $config_file);
    $tui->run();
    exit 0;
}

# non-interactive CLI mode
$format //= 'PDF';

if (@input_files == 0) {
    print "Error: No input files specified\n";
    pod2usage(2);
}

# validate format
my @valid_formats = qw/PDF HTML TXT MD RMD DOCX/;
$format = uc($format);
unless (grep { $_ eq $format } @valid_formats) {
    print "Error: Invalid format '$format'. Valid formats: " . join(', ', @valid_formats) . "\n";
    exit 1;
}

# process files
my $options = {
    log_level   => $verbose ? 'DEBUG' : ($quiet ? 'ERROR' : 'INFO'),
    config_file => $config_file,
};

# set jurisdiction in options if provided
if ($jurisdiction) {
    $options->{jurisdiction} = $jurisdiction;
}

for my $input_file (@input_files) {
    unless (-f $input_file) {
        print "Error: File '$input_file' not found\n";
        next;
    }

    my $output = $output_file;
    if ($batch_mode || (!$output_file && @input_files >= 1)) {
        my ($name, $path, $suffix) = fileparse($input_file, qr/\.[^.]*$/);
        my $ext = lc($format);
        $ext = 'Rmd' if $ext eq 'rmd';
        $output = "${path}${name}.${ext}";
    }

    unless ($output) {
        print "Error: Output file not specified\n";
        next;
    }

    eval {
        $options->{output_format} = $format;
        my $results = DC4U::process_dc_file($input_file, $format, $options);

        if ($results && @$results > 0) {
            if (@$results == 1) {
                open my $fh, '>', $output or die "Cannot write to $output: $!";
                binmode $fh if $format =~ /^(PDF|DOCX)$/;
                print $fh $results->[0]{output};
                close $fh;
                print "Generated: $output\n" unless $quiet;
            } else {
                # multi-charge: text formats concatenate, others split
                if ($format =~ /^(HTML|TXT|MD|RMD)$/) {
                    open my $fh, '>', $output or die "Cannot write to $output: $!";
                    for my $i (0 .. $#$results) {
                        print $fh "\n--- Charge " . ($i + 1) . " ---\n\n" if $i > 0;
                        print $fh $results->[$i]{output};
                    }
                    close $fh;
                    print "Generated: $output (${\ scalar(@$results)} charges)\n" unless $quiet;
                } else {
                    my ($name, $path, $suffix) = fileparse($output, qr/\.[^.]*$/);
                    for my $i (0 .. $#$results) {
                        my $cfile = "${path}${name}-charge-" . ($i + 1) . "${suffix}";
                        open my $fh, '>', $cfile or die "Cannot write to $cfile: $!";
                        binmode $fh if $format =~ /^(PDF|DOCX)$/;
                        print $fh $results->[$i]{output};
                        close $fh;
                        print "Generated: $cfile\n" unless $quiet;
                    }
                }
            }
        } else {
            print "Error: No output generated for $input_file\n";
        }
    };

    if ($@) {
        print "Error processing $input_file: $@\n";
    }
}

__END__

=head1 AUTHOR

DC4U Development Team

=head1 LICENSE

This software is licensed under the MIT License.

=cut
